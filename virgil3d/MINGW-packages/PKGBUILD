# Courtesy of qemu-3dfx <liewkj@yahoo.com>

_realname=virglrenderer
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=0.10.3
pkgrel=1
pkgdesc='A virtual 3D GPU library, that allows the guest operating system to use the host GPU to accelerate 3D rendering'
arch=('any')
mingw_arch=('mingw64')
url='https://virgil3d.github.io/'
license=(MIT)
depends=("${MINGW_PACKAGE_PREFIX}-libepoxy")
makedepends=("${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-ninja")
_tag=$pkgver
source=("virglrenderer-$pkgver.tar.bz2::https://gitlab.freedesktop.org/virgl/virglrenderer/-/archive/$_tag/virglrenderer-$_tag.tar.bz2"
        "0001-Virglrenderer-on-Windows-and-macOS.patch")
sha256sums=('70d362da3765139c74da00cf7fb2cac5d5dd92c112503ca7fff999032aeebf2f'
            '34448221077b1f204b85a7dd7591b6ee9356f37591aa730d9d44a6c45d80198c')

prepare() {
  cd virglrenderer-$_tag
  patch -p2 -i ${srcdir}/0001-Virglrenderer-on-Windows-and-macOS.patch
}

build() {
  cd virglrenderer-$_tag
  MSYS2_ARG_CONV_EXCL="--prefix" \
  meson --prefix="${MINGW_PREFIX}" build # -Dtests=true
  ninja -C build
}

package() {
  cd virglrenderer-$_tag
  DESTDIR="$pkgdir" ninja -C build install
  install -D -m644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}/${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
